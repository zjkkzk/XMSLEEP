plugins {
    id("com.android.application")
    id("org.jetbrains.kotlin.android")
    id("org.jetbrains.kotlin.plugin.compose")
    id("org.jetbrains.kotlin.plugin.serialization")
    id("com.google.dagger.hilt.android")
    id("org.jetbrains.kotlin.kapt")
    id("dagger.hilt.android.plugin")
}

android {
    namespace = "org.xmsleep.app"
    compileSdk = 35
    
    defaultConfig {
        applicationId = "org.xmsleep.app"
        minSdk = 26
        targetSdk = 35
		versionCode = 41
		versionName = "2.2.6"
        
        // 只保留 arm64-v8a 架构以减小 APK 体积（现代设备都支持）
        ndk {
            abiFilters += listOf("arm64-v8a")
            debugSymbolLevel = "NONE"  // 禁用debug符号，避免strip警告
        }
        
        // 从 gradle.properties 读取 GitHub Token（如果存在）
        val githubToken = project.findProperty("GITHUB_TOKEN") as String? ?: ""
        buildConfigField("String", "GITHUB_TOKEN", if (githubToken.isNotBlank()) "\"$githubToken\"" else "null")
    }
    
    signingConfigs {
        create("release") {
            val keystoreFile = project.findProperty("RELEASE_STORE_FILE") as String? ?: "${System.getProperty("user.home")}/XMSLEEP_KEYSTORE_SECURE/release.keystore"
            val keystorePassword = project.findProperty("RELEASE_STORE_PASSWORD") as String? ?: ""
            val keyAliasName = project.findProperty("RELEASE_KEY_ALIAS") as String? ?: "xmsleep"
            val keyAliasPassword = project.findProperty("RELEASE_KEY_PASSWORD") as String? ?: ""
            
            storeFile = file(keystoreFile)
            storePassword = keystorePassword
            keyAlias = keyAliasName
            keyPassword = keyAliasPassword
            enableV1Signing = true
            enableV2Signing = true
            enableV3Signing = true
            enableV4Signing = true
        }
    }
    
    buildTypes {
        release {
            isMinifyEnabled = true  // 启用代码混淆
            isShrinkResources = true  // 启用资源压缩
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
            signingConfig = signingConfigs.getByName("release")
        }
        debug {
            // 禁用debug版本的strip操作，避免警告
            isDebuggable = true
            ndk {
                debugSymbolLevel = "NONE"
            }
        }
    }
    
        
    buildFeatures {
        compose = true
        buildConfig = true
    }
    
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    
    kotlinOptions {
        jvmTarget = "17"
        freeCompilerArgs += listOf(
            "-opt-in=androidx.compose.material3.ExperimentalMaterial3Api",
            "-Xno-param-assertions",
            "-Xno-call-assertions",
            "-Xno-receiver-assertions"
        )
    }
    
    tasks.withType<org.jetbrains.kotlin.gradle.tasks.KotlinCompile>().configureEach {
        kotlinOptions {
            allWarningsAsErrors = false
        }
    }
    
    lint {
        disable.add("NullSafeMutableLiveData")
        disable.add("UnsafeOptInUsageError")
        disable.add("MissingTranslation")
    }
    
    packaging {
        // 忽略无法strip的库文件警告
        jniLibs {
            keepDebugSymbols.add("**/*.so")
            useLegacyPackaging = true
        }
    }
    
    dependenciesInfo {
        // Disables dependency metadata when building APKs (for IzzyOnDroid/F-Droid)
        includeInApk = false
        // Disables dependency metadata when building Android App Bundles (for Google Play)
        includeInBundle = false
    }
    
    testOptions {
        unitTests.isReturnDefaultValues = true
    }
}

dependencies {
    // Compose BOM
    implementation(platform("androidx.compose:compose-bom:2024.12.01"))
    androidTestImplementation(platform("androidx.compose:compose-bom:2024.12.01"))
    implementation("androidx.compose.ui:ui")
    implementation("androidx.compose.ui:ui-graphics")
    implementation("androidx.compose.ui:ui-tooling-preview")
    implementation("androidx.compose.material3:material3")
    implementation("androidx.compose.material:material-icons-extended")
    implementation("androidx.compose.foundation:foundation")
    // Accompanist
    implementation("com.google.accompanist:accompanist-swiperefresh:0.34.0")
    implementation("com.google.accompanist:accompanist-permissions:0.36.0") // 保持 0.36.0，与我们的 Compose BOM 2024.12.01 兼容
    
    // AndroidX
    implementation("androidx.core:core-ktx:1.15.0")
    implementation("androidx.core:core-splashscreen:1.0.1")
    implementation("androidx.appcompat:appcompat:1.7.1")
    implementation("androidx.activity:activity-compose:1.9.3")
    implementation("androidx.navigation:navigation-compose:2.9.5")
    
    // Kotlin
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:1.10.2")
    
    // Hilt - 依赖注入
    implementation("com.google.dagger:hilt-android:2.53.1")
    kapt("com.google.dagger:hilt-android-compiler:2.53.1")
    implementation("androidx.hilt:hilt-navigation-compose:1.2.0")
    
    // MaterialKolor - 动态主题色生成
    implementation("com.materialkolor:material-kolor:2.0.2")
    
    // Palette - 从图片提取主题色
    implementation("androidx.palette:palette-ktx:1.0.0")
    
    // Coil - 图片加载
    implementation("io.coil-kt:coil-compose:2.6.0")
    
    // Kotlin Serialization
    implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.9.0")
    
    // Lottie - 用于显示JSON动画
    implementation("com.airbnb.android:lottie:6.3.0")
    implementation("com.airbnb.android:lottie-compose:6.3.0")
    
    // ExoPlayer/Media3 - 用于无缝循环播放音频（声音模块需要）
    implementation("androidx.media3:media3-exoplayer:1.8.0")
    implementation("androidx.media3:media3-exoplayer-hls:1.8.0")
    implementation("androidx.media3:media3-ui:1.8.0")
    implementation("androidx.media3:media3-common:1.8.0")
    implementation("androidx.media3:media3-session:1.8.0")
    implementation("androidx.media:media:1.7.0")
    
    // OkHttp - 用于网络请求和文件下载
    implementation("com.squareup.okhttp3:okhttp:4.12.0")
    
    // Gson - 用于JSON解析
    implementation("com.google.code.gson:gson:2.10.1")
    
    // Haze - 用于实现真正的毛玻璃效果（backdrop blur）
    implementation("dev.chrisbanes.haze:haze:1.1.0")
    implementation("dev.chrisbanes.haze:haze-materials:1.1.0")
    
    // Debug
    debugImplementation("androidx.compose.ui:ui-tooling")
    
    // Testing
    testImplementation("junit:junit:4.13.2")
    testImplementation("io.mockk:mockk:1.13.8")
    testImplementation("org.jetbrains.kotlinx:kotlinx-coroutines-test:1.10.2")
    testImplementation("com.google.dagger:hilt-android-testing:2.53.1")
    kaptTest("com.google.dagger:hilt-android-compiler:2.53.1")
    
    // Android Testing
    androidTestImplementation("androidx.test.ext:junit:1.1.5")
    androidTestImplementation("androidx.test.espresso:espresso-core:3.5.1")
    androidTestImplementation("androidx.compose.ui:ui-test-junit4")
    androidTestImplementation("androidx.test:runner:1.5.2")
    androidTestImplementation("androidx.test:rules:1.5.0")
    androidTestImplementation("com.google.dagger:hilt-android-testing:2.53.1")
    kaptAndroidTest("com.google.dagger:hilt-android-compiler:2.53.1")
}

